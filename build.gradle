plugins {
    id 'eclipse'
    id 'java-library'
    id 'maven-publish'
}

group = 'com.glencoesoftware.omero'
version = '0.11.0'

sourceCompatibility = 1.8
targetCompatibility = 1.8

repositories {
    mavenCentral()
    maven {
        name 'Unidata'
        url 'https://artifacts.unidata.ucar.edu/content/repositories/unidata-releases'
    }
    maven {
        url 'https://artifacts.glencoesoftware.com/artifactory/ome.releases'
    }
}

configurations.all {
    resolutionStrategy.cacheChangingModulesFor 0, 'minutes'
    exclude group: 'com.github.marcus-nl.btm'
    exclude group: 'com.codahale.metrics'
    exclude group: 'com.drewnoakes'
    exclude group: 'com.esotericsoftware.kryo'
    exclude group: 'com.google.guava'
    exclude group: 'com.jamonapi'
    exclude group: 'com.mortennobel'
    exclude group: 'com.zeroc', module: 'icestorm'
    exclude group: 'commons-beanutils'
    exclude group: 'commons-codec'
    exclude group: 'commons-io'
    exclude group: 'edu.ucar'
    exclude group: 'geronimo-spec'
    exclude group: 'gnu.getopt'
    exclude group: 'joda-time'
    exclude group: 'net.sf.ehcache'
    exclude group: 'ome', module: 'formats-gpl'
    exclude group: 'ome', module: 'turbojpeg'
    exclude group: 'org.apache.commons'
    exclude group: 'org.apache.httpcomponents'
    exclude group: 'org.apache.pdfbox'
    exclude group: 'org.apache.xmlgraphics'
    exclude group: 'org.hibernate'
    exclude group: 'org.ini4j'
    exclude group: 'org.javassist'
    exclude group: 'org.quartz-scheduler'
    exclude group: 'org.scijava'
    exclude group: 'org.subethamail'
    exclude group: 'xerces'
    exclude group: 'xalan'
}

dependencies {
    implementation 'io.prometheus:simpleclient:0.6.0'
    implementation 'io.zipkin.brave:brave:5.6.8'
    implementation 'io.zipkin.brave:brave-http:4.13.6'
    implementation 'io.zipkin.brave:brave-instrumentation-http:5.6.8'
    implementation 'io.lettuce:lettuce-core:6.7.1.RELEASE'
    implementation 'org.openmicroscopy:omero-blitz:5.8.3'
    implementation 'io.vertx:vertx-web:4.5.16'
    implementation 'io.vertx:vertx-jdbc-client:4.5.16'
    implementation 'io.kaitai:kaitai-struct-runtime:0.8'
    implementation 'commons-lang:commons-lang:2.6'
    implementation 'com.zeroc:icegrid:3.6.5'
    implementation 'org.slf4j:slf4j-api:2.0.7'

    testImplementation 'org.testng:testng:6.10'
}

test {
  // enable TestNG support (default is JUnit)
  useTestNG()
}

publishing {
    publications {
        omeroMsCore(MavenPublication) {
            from components.java
        }
    }

    repositories {
        maven {
            name 'gs-omero-snapshots-local'
            url 'https://artifacts.glencoesoftware.com/artifactory/gs-omero-snapshots-local'
            credentials {
               username = project.properties.ArtifactoryUserName
               password = project.properties.ArtifactoryPassword
            }
        }
    }
}
